plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.siouan.frontend-jdk17" version "8.0.0"
}

group = 'com.greglturnquist'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

frontend {
    nodeVersion = "16.20.2"
    assembleScript = "run build"
    cleanScript = "run clean"
    checkScript = "run check"
}

tasks.named('test') {
    useJUnitPlatform()
}

task copyJarToDist(type: Copy) {
    group = 'Distribution'
    description = 'Copy the generated JAR files to the dist folder'

    // Define the source directory
    def sourceJars = fileTree('build/libs/').include('*.jar')

    // Define the destination directory for the copy
    def distDir = file("${project.rootDir}/dist")

    // Configure the copy operation
    from sourceJars
    into distDir
}

